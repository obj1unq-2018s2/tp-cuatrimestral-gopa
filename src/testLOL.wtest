import campeones.*
import items.*

describe "test de liga de leyendas"{
	
	var anilloDeDoran=  new AnilloDeDoran()
	var anilloDeDoran2= new AnilloDeDoran()
	
	var tomoAmplificador = new TomoAmplificador()
	
//	var sombreroDeRabadon = new SombreroDeRabadon()
	
	var pocionDeVida = new PocionDeVida()
	
	var campeon1 = new Campeon(
		puntosDeVida = 50,
		ataque = 15,
		dinero = 800
	)
	var campeon2 = new Campeon(
		puntosDeVida = 50,
		ataque = 20,
		dinero = 300
	)
	
	var bastonDelVacio= new BastonDelVacio(
		materiales=[tomoAmplificador,pocionDeVida]
	)
	
	const oleada1 = new OleadaMinion(
		plusDeAtaque = 25,
		cantMinions = 10
	)
	const oleada2 = new OleadaMinion(
		plusDeAtaque = 5,
		cantMinions = 20
	)
	const oleada3 = new OleadaMinion(
		plusDeAtaque = 15,
		cantMinions = 10
	)
	const ejercito = new EjercitoDeMinions(
		oleadas = [oleada2, oleada3]
	)
	
	test "estado inicial del campe√≥n"{
		assert.equals(50, campeon1.vidaTotal())
		assert.equals(15, campeon1.ataqueTotal() )
	}
	test "estado inicial Oleada de minions"{
		assert.equals(25, oleada1.plusDeAtaque())
		assert.equals(10, oleada1.cantMinions())
	}
	test "equipa anillo de doran"{
		campeon1.equiparItem(anilloDeDoran)
		assert.equals([anilloDeDoran], campeon1.items())	
		assert.equals(5, campeon1.puntosDeDanioTotal())
		assert.equals(110, campeon1.vidaTotal())
		assert.equals(30, campeon1.ataqueTotal() )
	
	}
	test "equipa anillo de doran y tomo amplificador"{
		campeon1.equiparItem(anilloDeDoran)
		campeon1.equiparItem(tomoAmplificador)
		assert.equals([anilloDeDoran, tomoAmplificador],campeon1.items())
		assert.equals(5, campeon1.puntosDeDanioTotal())
		assert.equals(111.25, campeon1.vidaTotal())
		assert.equals(30.25, campeon1.ataqueTotal())
		assert.equals(2, campeon1.bloqueoTotal())
	
	}
	test "campeon ataca a una oleada de minions"{
		campeon1.bloqueo(1)
		campeon1.atacar(oleada1)
		assert.equals(0, campeon1.bloqueo())
		campeon1.atacar(oleada1)
		campeon1.atacar(oleada1)
		assert.equals(0,oleada1.cantMinions())
		assert.equals(50, campeon1.puntosDeDanioTotal())
		assert.equals(810,campeon1.dinero())
		
	}
	test " campeon se desequipa el tomo amplificador"{
		campeon1.equiparItem(anilloDeDoran)
		campeon1.equiparItem(tomoAmplificador)
		campeon1.desequiparItem(tomoAmplificador)
		assert.equals([anilloDeDoran], campeon1.items())
		assert.equals(1, campeon1.bloqueo())
		assert.equals(35,campeon1.puntosDeDanioTotal())
	}
	test "campeon se desequipa el tomo amplificador y el anillo de doran"{
		campeon1.equiparItem(anilloDeDoran)
		campeon1.equiparItem(tomoAmplificador)
		campeon1.desequiparItem(tomoAmplificador)		
		campeon1.desequiparItem(anilloDeDoran)
		assert.equals([],campeon1.items())
		assert.equals(50,campeon1.vidaTotal())
		assert.equals(20,campeon1.puntosDeDanioTotal())
	}
	test "campeon equipa pocion de vida"{
		campeon1.puntosDeDanio(150)
		campeon1.equiparItem(pocionDeVida)
		campeon1.activarHabilidad(pocionDeVida)
		campeon1.activarHabilidad(pocionDeVida)
		campeon1.activarHabilidad(pocionDeVida)
		assert.equals(50, campeon1.puntosDeDanioTotal())
		
	}
	
	test "campeon muere (test pedido)"{
		
		campeon1.equiparItem(anilloDeDoran)
		campeon1.equiparItem(tomoAmplificador)
		campeon1.atacar(oleada1)
		campeon1.atacar(oleada1)
		campeon1.atacar(oleada1)
		campeon1.desequiparItem(anilloDeDoran)
		campeon1.desequiparItem(tomoAmplificador)
		campeon1.equiparItem(tomoAmplificador)
		campeon1.desequiparItem(tomoAmplificador)	
		assert.that( campeon1.estaMuerto() )
		
	}

	test " baston del vacio "{
		campeon1.comprar(anilloDeDoran)
		campeon1.comprar(anilloDeDoran2)
		assert.equals(10, campeon1.puntosDeDanioTotal() )
		campeon1.equiparItem(bastonDelVacio)
		campeon1.activarHabilidad(bastonDelVacio)
		assert.that( bastonDelVacio.materiales().get(1).uso() == 1 )
		assert.equals(0, campeon1.puntosDeDanio() )
		
	}
	test "ejercito de minions (test pedido)"{
		campeon2.atacar(ejercito)
		assert.that(campeon2.estaMuerto())
		assert.that(ejercito.todosMuertos())
		assert.equals(330, campeon2.dinero())
	}
}




